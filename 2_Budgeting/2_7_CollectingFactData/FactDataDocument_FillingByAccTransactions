//процедура для использования в произвольном коде в справочнике
//e1cib/list/Справочник.ПравилаЗаполненияОбъектовБД
//добавлено по задаче SSC-444 для универсализации и возможности быстрого редактирования специалистом от финансов. 
Процедура ЗаполнитьДокументОтражениеФактическихДанныхПоПроводкамБухУчета(ИсходныйОбъектСсылка,ДокументОбъект,ИмяТипаДокумента,НомерЗадачи) Экспорт
	
Если ИсходныйОбъектСсылка = неопределено тогда 
	ИсходныйОбъектСсылка = ДокументОбъект.ИсходныйДокумент;
КонецЕсли;
	
ЗначениеДатыФакта = УправлениеСвойствами.ЗначениеСвойства(ИсходныйОбъектСсылка,ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ДатаОФД"));
Если значениеЗаполнено(ЗначениеДатыФакта) тогда 
ДокументОбъект.Дата =  ЗначениеДатыФакта;
Иначе 
ДокументОбъект.Дата = ИсходныйОбъектСсылка.Дата;
КонецЕсли;

ДокументОбъект.ДокументБД = Справочники.ДокументыБД.НайтиПоНаименованию(ИмяТипаДокумента);


БюджетДоходовИРасходов  = ДокументОбъект.БюджетДоходовИРасходов; //получаем табчасть БДиР
БюджетДоходовИРасходов.очистить();

ДокументОбъект.ИсходныйДокумент = ИсходныйОбъектСсылка;
ДокументОбъект.Организация = ИсходныйОбъектСсылка.Организация;

ДокументОбъект.Комментарий = НомерЗадачи+" "+ Строка(ТекущаяДата());

Запрос = Новый Запрос;
Запрос.Текст="ВЫБРАТЬ
|	ХозрасчетныйОборотыДтКт.СчетДт КАК СчетЗатрат,
|	ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК СубконтоДт1,
|	ХозрасчетныйОборотыДтКт.СубконтоДт2 КАК СубконтоДт2,
|	ХозрасчетныйОборотыДтКт.СубконтоДт3 КАК СубконтоДт3,
|	ХозрасчетныйОборотыДтКт.Регистратор КАК Регистратор,
|	ХозрасчетныйОборотыДтКт.Период КАК Период,
|	ХозрасчетныйОборотыДтКт.СуммаОборот КАК Сумма
|ПОМЕСТИТЬ ВТ_ПроводкиДокумента
|ИЗ
|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачПериода, &КонПериода, Запись, , , , , ) КАК ХозрасчетныйОборотыДтКт
|ГДЕ
|	ХозрасчетныйОборотыДтКт.Регистратор = &Регистратор
|	И ХозрасчетныйОборотыДтКт.Регистратор ССЫЛКА Документ.%ИмяТипаДокумента%
|
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ВТ_ПроводкиДокумента.Регистратор КАК Регистратор,
|	ВТ_ПроводкиДокумента.Период КАК Период,
|	ВТ_ПроводкиДокумента.Сумма КАК Сумма,
|	ВЫБОР
|		КОГДА ВТ_ПроводкиДокумента.СчетЗатрат = &Счет20
|			ТОГДА ВТ_ПроводкиДокумента.СубконтоДт1
|		ИНАЧЕ ЗНАЧЕНИЕ(справочник.НоменклатурныеГруппы.ПустаяСсылка)
|	КОНЕЦ КАК НоменклатурнаяГруппа,
|	ВЫБОР
|		КОГДА ВТ_ПроводкиДокумента.СчетЗатрат = &Счет20
|			ТОГДА ВТ_ПроводкиДокумента.СубконтоДт2
|		КОГДА ВТ_ПроводкиДокумента.СчетЗатрат В (&СписокСчетовОПХР)
|			ТОГДА ВТ_ПроводкиДокумента.СубконтоДт1
|		ИНАЧЕ ЗНАЧЕНИЕ(справочник.СтатьиЗатрат.ПустаяСсылка)
|	КОНЕЦ КАК СтатьяЗатрат,
|	ВТ_ПроводкиДокумента.СчетЗатрат КАК СчетЗатрат
|ПОМЕСТИТЬ ВТ_ПроводкиСТипамиСубконто
|ИЗ
|	ВТ_ПроводкиДокумента КАК ВТ_ПроводкиДокумента
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ВТ_ПроводкиСТипамиСубконто.Регистратор КАК Регистратор,
|	ВТ_ПроводкиСТипамиСубконто.Период КАК Период,
|	ВТ_ПроводкиСТипамиСубконто.Сумма КАК Сумма,
|	ВТ_ПроводкиСТипамиСубконто.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
|	ВТ_ПроводкиСТипамиСубконто.СтатьяЗатрат КАК СтатьяЗатрат,
|	ВТ_ПроводкиСТипамиСубконто.СчетЗатрат КАК СчетЗатрат,
|	НоменклатурныеГруппыДополнительныеРеквизиты.Значение КАК Проект,
|	СтатьиЗатратДополнительныеРеквизиты.Значение КАК СтатьяИсполнение
|ИЗ
|	ВТ_ПроводкиСТипамиСубконто КАК ВТ_ПроводкиСТипамиСубконто
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатурныеГруппы.ДополнительныеРеквизиты КАК НоменклатурныеГруппыДополнительныеРеквизиты
|		ПО (ВТ_ПроводкиСТипамиСубконто.НоменклатурнаяГруппа = НоменклатурныеГруппыДополнительныеРеквизиты.Ссылка)
|			И (НоменклатурныеГруппыДополнительныеРеквизиты.Свойство.Имя = ""Проект"")
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиЗатрат.ДополнительныеРеквизиты КАК СтатьиЗатратДополнительныеРеквизиты
|		ПО (ВТ_ПроводкиСТипамиСубконто.СтатьяЗатрат = СтатьиЗатратДополнительныеРеквизиты.Ссылка)
|			И (СтатьиЗатратДополнительныеРеквизиты.Свойство.Имя = ""СтатьяДоходовИРасходов"")
|ГДЕ
|	ВТ_ПроводкиСТипамиСубконто.СтатьяЗатрат ССЫЛКА Справочник.СтатьиЗатрат
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	ВТ_ПроводкиСТипамиСубконто.Регистратор,
|	ВТ_ПроводкиСТипамиСубконто.Период,
|	ВТ_ПроводкиСТипамиСубконто.Сумма,
|	ВТ_ПроводкиСТипамиСубконто.НоменклатурнаяГруппа,
|	ВТ_ПроводкиСТипамиСубконто.СтатьяЗатрат,
|	ВТ_ПроводкиСТипамиСубконто.СчетЗатрат,
|	НоменклатурныеГруппыДополнительныеРеквизиты.Значение,
|	ПрочиеДоходыИРасходыДополнительныеРеквизиты.Значение
|ИЗ
|	ВТ_ПроводкиСТипамиСубконто КАК ВТ_ПроводкиСТипамиСубконто
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатурныеГруппы.ДополнительныеРеквизиты КАК НоменклатурныеГруппыДополнительныеРеквизиты
|		ПО (ВТ_ПроводкиСТипамиСубконто.НоменклатурнаяГруппа = НоменклатурныеГруппыДополнительныеРеквизиты.Ссылка
|				И НоменклатурныеГруппыДополнительныеРеквизиты.Свойство.Имя = ""Проект"")
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПрочиеДоходыИРасходы.ДополнительныеРеквизиты КАК ПрочиеДоходыИРасходыДополнительныеРеквизиты
|		ПО (ВТ_ПроводкиСТипамиСубконто.СтатьяЗатрат = ПрочиеДоходыИРасходыДополнительныеРеквизиты.Ссылка
|				И ПрочиеДоходыИРасходыДополнительныеРеквизиты.Свойство.Имя = ""СтатьяДоходовИРасходов"")
|ГДЕ
|	ВТ_ПроводкиСТипамиСубконто.СтатьяЗатрат ССЫЛКА Справочник.ПрочиеДоходыИРасходы";


Запрос.Текст = СтрЗаменить(Запрос.Текст,"%ИмяТипаДокумента%",ИмяТипаДокумента);

// Присвоение значений переменным параметров.
СписокСчетовОПХР = Новый Массив;
СписокСчетовОПХР.Добавить(ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы); // 26 Общехозяйственные расходы
СписокСчетовОПХР.Добавить(ПланыСчетов.Хозрасчетный.ИздержкиОбращения); // 44.01 Издержки обращения в организациях, осуществляющих торговую деятельность
СписокСчетовОПХР.Добавить(ПланыСчетов.Хозрасчетный.КоммерческиеРасходы); // 44.02 
СписокСчетовОПХР.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасходы); // 91.02 Прочие расходы

Счет20 = ПланыСчетов.Хозрасчетный.ОсновноеПроизводство; // 20.01 Основное производство


// Установка параметров.
Запрос.УстановитьПараметр("СписокСчетовОПХР", СписокСчетовОПХР);
Запрос.УстановитьПараметр("Регистратор", ИсходныйОбъектСсылка);
Запрос.УстановитьПараметр("НачПериода",ИсходныйОбъектСсылка.Дата);
Запрос.УстановитьПараметр("КонПериода",КонецДня(ИсходныйОбъектСсылка.Дата));
Запрос.УстановитьПараметр("Счет20", Счет20);


лВыборка =Запрос.Выполнить().Выбрать();

БюджетДоходовИРасходов.Очистить();
	
Пока лВыборка.Следующий() Цикл

		Если значениезаполнено(лВыборка.СтатьяИсполнение) тогда
		лНоваяСтрока = БюджетДоходовИРасходов.Добавить();
		лНоваяСтрока.СтатьяДоходовИРасходов         = лВыборка.СтатьяИсполнение;
		лНоваяСтрока.Проект               = лВыборка.Проект;
		лНоваяСтрока.СуммаБезНДС          = лВыборка.Сумма;
		лНоваяСтрока.СуммаНДС             =    0;
		лНоваяСтрока.Сумма                =        лВыборка.Сумма;
		
	
		//добавить условие по валюте
		лНоваяСтрока.СуммаВзаиморасчетовБезНДС   = лНоваяСтрока.СуммаБезНДС;
		лНоваяСтрока.СуммаВзаиморасчетовНДС         = лНоваяСтрока.СуммаНДС;
		лНоваяСтрока.СуммаВзаиморасчетов                 =лНоваяСтрока.Сумма;
	    конецесли;
		
		
КонецЦикла;
ДокументОбъект.СуммаДокумента= БюджетДоходовИРасходов.Итог("Сумма");
	
КонецПроцедуры	
