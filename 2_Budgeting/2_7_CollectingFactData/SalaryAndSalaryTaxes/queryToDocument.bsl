ВЫБРАТЬ
	СтатьиЗатратДополнительныеРеквизиты.Ссылка КАК Статья,
	СтатьиЗатратДополнительныеРеквизиты.Значение КАК СтатьяДоходовИРасходов
ПОМЕСТИТЬ ВТ_СоответствиеСтатей
ИЗ
	Справочник.СтатьиЗатрат.ДополнительныеРеквизиты КАК СтатьиЗатратДополнительныеРеквизиты
ГДЕ
	СтатьиЗатратДополнительныеРеквизиты.Свойство.Имя = "СтатьяДоходовИРасходов"
	И СтатьиЗатратДополнительныеРеквизиты.Ссылка.ЭтоГруппа = ЛОЖЬ

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ПрочиеДоходыИРасходыДополнительныеРеквизиты.Ссылка,
	ПрочиеДоходыИРасходыДополнительныеРеквизиты.Значение
ИЗ
	Справочник.ПрочиеДоходыИРасходы.ДополнительныеРеквизиты КАК ПрочиеДоходыИРасходыДополнительныеРеквизиты
ГДЕ
	ПрочиеДоходыИРасходыДополнительныеРеквизиты.Свойство.Имя = "СтатьяДоходовИРасходов"
	И ПрочиеДоходыИРасходыДополнительныеРеквизиты.Ссылка.ЭтоГруппа = ЛОЖЬ
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
	СУММА(ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.Сумма) КАК Сумма,
	ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	ПорядокОтраженияЗарплатыВБухУчете.Счет КАК Счет,
	ПорядокОтраженияЗарплатыВБухУчете.Субконто1 КАК Субконто1,
	ПорядокОтраженияЗарплатыВБухУчете.Субконто2 КАК Субконто2,
	ПорядокОтраженияЗарплатыВБухУчете.Субконто3 КАК Субконто3,
	СУММА(ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.ПФРПоСуммарномуТарифу + ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.ПФРДоПредельнойВеличины + ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.ПФРСПревышения + ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.ПФРСтраховая + ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.ПФРНакопительная + ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.ФСС + ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.ФФОМС + ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.ТФОМС) КАК СтраховыеВзносы,
	СУММА(ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи
ПОМЕСТИТЬ ВТ_СтрокиДокумента
ИЗ
	Документ.ОтражениеЗарплатыВБухучете.НачисленнаяЗарплатаИВзносы КАК ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияЗарплатыВБухУчете КАК ПорядокОтраженияЗарплатыВБухУчете
		ПО ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете = ПорядокОтраженияЗарплатыВБухУчете.СпособОтраженияЗарплатыВБухУчете
ГДЕ
	ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.ФизическоеЛицо,
	ОтражениеЗарплатыВБухучетеНачисленнаяЗарплатаИВзносы.СпособОтраженияЗарплатыВБухучете,
	ПорядокОтраженияЗарплатыВБухУчете.Счет,
	ПорядокОтраженияЗарплатыВБухУчете.Субконто1,
	ПорядокОтраженияЗарплатыВБухУчете.Субконто2,
	ПорядокОтраженияЗарплатыВБухУчете.Субконто3
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_СтрокиДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
	ВТ_СтрокиДокумента.Сумма КАК Сумма,
	ВТ_СтрокиДокумента.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	ВТ_СтрокиДокумента.Счет КАК Счет,
	ВТ_СтрокиДокумента.СтраховыеВзносы КАК СтраховыеВзносы,
	ВТ_СтрокиДокумента.ФССНесчастныеСлучаи КАК ФССНесчастныеСлучаи,
	ВЫБОР
		КОГДА ВТ_СтрокиДокумента.Счет.Код В ("26", "44.01", "44.02", "91.02")
			ТОГДА ВТ_СтрокиДокумента.Субконто1
		КОГДА ВТ_СтрокиДокумента.Счет.Код = "20.01"
			ТОГДА ВТ_СтрокиДокумента.Субконто2
		ИНАЧЕ СтатьяПоУмолчанию.Ссылка
	КОНЕЦ КАК СтатьяЗатрат
ПОМЕСТИТЬ ВТ_СтрокиСоСтатьей
ИЗ
	ВТ_СтрокиДокумента КАК ВТ_СтрокиДокумента,
	(ВЫБРАТЬ
		СтатьиЗатрат.Ссылка КАК Ссылка
	ИЗ
		Справочник.СтатьиЗатрат КАК СтатьиЗатрат
	ГДЕ
		СтатьиЗатрат.Ссылка = &СтатьяПоУмолчанию) КАК СтатьяПоУмолчанию
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_СтрокиСоСтатьей.ФизическоеЛицо КАК ФизическоеЛицо,
	ВТ_СтрокиСоСтатьей.Сумма КАК Сумма,
	ВТ_СтрокиСоСтатьей.Счет КАК Счет,
	ВТ_СтрокиСоСтатьей.СтраховыеВзносы КАК СтраховыеВзносы,
	ВТ_СтрокиСоСтатьей.ФССНесчастныеСлучаи КАК ФССНесчастныеСлучаи,
	ВТ_СтрокиСоСтатьей.СтатьяЗатрат КАК СтатьяЗатрат,
	СтраховыеВзносыСтатья.СтатьяЗатрат КАК СтатьяЗатратВзносы,
	ФСССтатья.СтатьяЗатрат КАК СтатьяЗатратФСС
ПОМЕСТИТЬ ВТ_1
ИЗ
	ВТ_СтрокиСоСтатьей КАК ВТ_СтрокиСоСтатьей
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатьиЗатратПоЗарплате КАК СтраховыеВзносыСтатья
		ПО ВТ_СтрокиСоСтатьей.СтатьяЗатрат = СтраховыеВзносыСтатья.СтатьяЗатратНачисления
			И (СтраховыеВзносыСтатья.ВидСтатьиЗатрат = ЗНАЧЕНИЕ(перечисление.ВидыДополнительныхСтатейЗатрат.СтраховыеВзносы))
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатьиЗатратПоЗарплате КАК ФСССтатья
		ПО ВТ_СтрокиСоСтатьей.СтатьяЗатрат = ФСССтатья.СтатьяЗатратНачисления
			И (ФСССтатья.ВидСтатьиЗатрат = ЗНАЧЕНИЕ(перечисление.ВидыДополнительныхСтатейЗатрат.ФСС_НС))
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_1.ФизическоеЛицо КАК ФизическоеЛицо,
	СУММА(ВТ_1.Сумма) КАК Сумма,
	СУММА(ВТ_1.СтраховыеВзносы) КАК СтраховыеВзносы,
	СУММА(ВТ_1.ФССНесчастныеСлучаи) КАК ФССНесчастныеСлучаи,
	ВТ_1.СтатьяЗатрат КАК СтатьяЗатрат,
	ВТ_1.СтатьяЗатратВзносы КАК СтатьяЗатратВзносы,
	ВТ_1.СтатьяЗатратФСС КАК СтатьяЗатратФСС,
	ВТ_СоответствиеСтатей.СтатьяДоходовИРасходов КАК СтатьяДоходовИРасходовЗП,
	ВТ_СоответствиеСтатей1.СтатьяДоходовИРасходов КАК СтатьяДоходовИРасходовСтрахВзносы,
	ВТ_СоответствиеСтатей2.СтатьяДоходовИРасходов КАК СтатьяДоходовИРасходов_ФСС
ИЗ
	ВТ_1 КАК ВТ_1
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СоответствиеСтатей КАК ВТ_СоответствиеСтатей
		ПО ВТ_1.СтатьяЗатрат = ВТ_СоответствиеСтатей.Статья
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СоответствиеСтатей КАК ВТ_СоответствиеСтатей1
		ПО ВТ_1.СтатьяЗатратВзносы = ВТ_СоответствиеСтатей1.Статья
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СоответствиеСтатей КАК ВТ_СоответствиеСтатей2
		ПО ВТ_1.СтатьяЗатратФСС = ВТ_СоответствиеСтатей2.Статья

СГРУППИРОВАТЬ ПО
	ВТ_1.ФизическоеЛицо,
	ВТ_1.СтатьяЗатрат,
	ВТ_1.СтатьяЗатратВзносы,
	ВТ_1.СтатьяЗатратФСС,
	ВТ_СоответствиеСтатей.СтатьяДоходовИРасходов,
	ВТ_СоответствиеСтатей1.СтатьяДоходовИРасходов,
	ВТ_СоответствиеСтатей2.СтатьяДоходовИРасходов
